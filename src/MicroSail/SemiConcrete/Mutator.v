(******************************************************************************)
(* Copyright (c) 2020 Steven Keuchel                                          *)
(* All rights reserved.                                                       *)
(*                                                                            *)
(* Redistribution and use in source and binary forms, with or without         *)
(* modification, are permitted provided that the following conditions are     *)
(* met:                                                                       *)
(*                                                                            *)
(* 1. Redistributions of source code must retain the above copyright notice,  *)
(*    this list of conditions and the following disclaimer.                   *)
(*                                                                            *)
(* 2. Redistributions in binary form must reproduce the above copyright       *)
(*    notice, this list of conditions and the following disclaimer in the     *)
(*    documentation and/or other materials provided with the distribution.    *)
(*                                                                            *)
(* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        *)
(* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED  *)
(* TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR *)
(* PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR          *)
(* CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,      *)
(* EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,        *)
(* PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR         *)
(* PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF     *)
(* LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING       *)
(* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         *)
(* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               *)
(******************************************************************************)

From Coq Require Import
     Bool.Bool
     Lists.List
     Logic.EqdepFacts
     Program.Equality
     Program.Tactics
     Strings.String
     Arith.PeanoNat
     ZArith.ZArith.

From Equations Require Import Equations.

From MicroSail Require Import
     Sep.Spec
     Symbolic.Outcome
     Syntax.

From stdpp Require Import base list option.

Import CtxNotations.
Import EnvNotations.
Import ListNotations.
Import OutcomeNotations.

Set Implicit Arguments.

Delimit Scope mutator_scope with mut.
Delimit Scope dmut_scope with dmut.

Module SemiConcrete
       (typekit : TypeKit)
       (termkit : TermKit typekit)
       (progkit : ProgramKit typekit termkit)
       (assertkit : AssertionKit typekit termkit progkit)
       (symcontractkit : SymbolicContractKit typekit termkit progkit assertkit).

  Export symcontractkit.

  Inductive SCChunk : Type :=
  | scchunk_pred   (p : ùë∑) (vs : Env Lit (ùë∑_Ty p))
  | scchunk_ptsreg {œÉ : Ty} (r : ùëπùë¨ùëÆ œÉ) (v : Lit œÉ).
  Arguments scchunk_pred _ _ : clear implicits.

  Definition SCHeap  : Type :=
    list SCChunk.

  Section SemiConcreteState.

    Local Set Primitive Projections.

    Record SCState (Œì : Ctx (ùëø * Ty)) : Type :=
      MkSCState
        { scstate_localstore    : LocalStore Œì;
          scstate_heap          : SCHeap;
        }.
    Global Arguments scstate_localstore {_} _.
    Global Arguments scstate_heap {_} _.

    Definition scstate_initial {Œì} (Œ¥ : LocalStore Œì) : SCState Œì :=
      MkSCState Œ¥ nil.

  End SemiConcreteState.

  Section ChunkExtraction.

    Fixpoint heap_extractions (h : SCHeap) : list (SCChunk * SCHeap) :=
      match h with
      | []     => []
      | c :: h => (c , h) :: map (fun '(c', h') => (c' , c :: h')) (heap_extractions h)
      end.

    Equations(noeqns) match_chunk_eqb (ce : SCChunk) (cr : SCChunk) : bool :=
      match_chunk_eqb (scchunk_pred p1 vs1) (scchunk_pred p2 vs2)
      with ùë∑_eq_dec p1 p2 => {
        match_chunk_eqb (scchunk_pred p1 vs1) (scchunk_pred p2 vs2) (left eq_refl) := env_beq Lit_eqb vs1 vs2;
        match_chunk_eqb (scchunk_pred p1 vs1) (scchunk_pred p2 vs2) (right _) := false
      };
      match_chunk_eqb (scchunk_ptsreg r1 t1) (scchunk_ptsreg r2 t2)
      with ùëπùë¨ùëÆ_eq_dec r1 r2 => {
        match_chunk_eqb (scchunk_ptsreg r1 v1) (scchunk_ptsreg r2 v2) (left (@teq_refl eq_refl eq_refl)) := Lit_eqb _ v1 v2;
        match_chunk_eqb (scchunk_ptsreg r1 v1) (scchunk_ptsreg r2 v2) (right _)      := false
      };
      match_chunk_eqb _ _  := false.

    Definition extract_chunk_eqb (ce : SCChunk) (h : SCHeap) : list SCHeap :=
      List.map snd (List.filter (fun '(cr,_) => match_chunk_eqb ce cr) (heap_extractions h)).

  End ChunkExtraction.

  Section SemiConcreteMutatorResult.

    Local Set Primitive Projections.
    Local Set Maximal Implicit Insertion.

    Record SCMutResult (Œì : Ctx (ùëø * Ty)) (A : Type) : Type :=
      MkSCMutResult {
          scmutres_value : A;
          scmutres_state : SCState Œì;
        }.

  End SemiConcreteMutatorResult.

  Section SemiConcreteMutator.

    Definition SCMut (Œì1 Œì2 : Ctx (ùëø * Ty)) (A : Type) : Type :=
      SCState Œì1 -> Outcome (SCMutResult Œì2 A).
    Bind Scope mutator_scope with SCMut.

    Definition scmut_demonic {Œì1 Œì2 I A} (ms : I -> SCMut Œì1 Œì2 A) : SCMut Œì1 Œì2 A :=
      fun (s : SCState Œì1) => (‚®Ç i : I => ms i s)%out.
    Definition scmut_angelic {Œì1 Œì2 I A} (ms : I -> SCMut Œì1 Œì2 A) : SCMut Œì1 Œì2 A :=
      fun (s : SCState Œì1) => (‚®Å i : I => ms i s)%out.
    Definition scmut_fail {Œì1 Œì2 A} (msg : string) : SCMut Œì1 Œì2 A :=
      fun s => outcome_fail msg.
    Definition scmut_block {Œì1 Œì2 A} : SCMut Œì1 Œì2 A :=
      fun s => outcome_block.

    Definition scmut_demonic_binary {Œì1 Œì2 A} (m1 m2 : SCMut Œì1 Œì2 A) : SCMut Œì1 Œì2 A :=
      fun s => outcome_demonic_binary (m1 s) (m2 s).
    Definition scmut_angelic_binary {Œì1 Œì2 A} (m1 m2 : SCMut Œì1 Œì2 A) : SCMut Œì1 Œì2 A :=
      fun s => outcome_angelic_binary (m1 s) (m2 s).

    Definition scmut_pure {Œì A} (a : A) : SCMut Œì Œì A :=
      fun s => outcome_pure (MkSCMutResult a s).
    Definition scmut_bind {Œì1 Œì2 Œì3 A B} (ma : SCMut Œì1 Œì2 A) (f : A -> SCMut Œì2 Œì3 B) : SCMut Œì1 Œì3 B :=
      fun s0 => outcome_bind (ma s0) (fun '(MkSCMutResult a s1) => f a s1).
    Definition scmut_bind_right {Œì1 Œì2 Œì3 A B} (ma : SCMut Œì1 Œì2 A) (mb : SCMut Œì2 Œì3 B) : SCMut Œì1 Œì3 B :=
      scmut_bind ma (fun _ => mb).
    Definition scmut_bind_left {Œì1 Œì2 Œì3 A B} (ma : SCMut Œì1 Œì2 A) (mb : SCMut Œì2 Œì3 B) : SCMut Œì1 Œì3 A :=
      scmut_bind ma (fun a => scmut_bind mb (fun _ => scmut_pure a)).
    Definition scmut_map {Œì1 Œì2 A B} (f : A -> B) (ma : SCMut Œì1 Œì2 A) : SCMut Œì1 Œì2 B :=
      scmut_bind ma (fun a => scmut_pure (f a)).
    Definition scmut_angelic_list {Œì A} (msg : string) :
      list A -> SCMut Œì Œì A :=
      fix scmut_angelic_list (xs : list A) :=
        match xs with
        | []      => scmut_fail msg
        | x :: [] => scmut_pure x
        | x :: xs => scmut_angelic_binary (scmut_pure x) (scmut_angelic_list xs)
        end.

  End SemiConcreteMutator.
  Bind Scope mutator_scope with SCMut.

  Module MutatorNotations.

    Notation "'‚®Ç' x .. y => F" :=
      (scmut_demonic (fun x => .. (scmut_demonic (fun y => F)) .. )) : mutator_scope.

    Notation "'‚®Å' x .. y => F" :=
      (scmut_angelic (fun x => .. (scmut_angelic (fun y => F)) .. )) : mutator_scope.

    Infix "‚äó" := scmut_demonic_binary (at level 40, left associativity) : mutator_scope.
    Infix "‚äï" := scmut_angelic_binary (at level 50, left associativity) : mutator_scope.

    Notation "x <- ma ;; mb" :=
      (scmut_bind ma (fun x => mb))
        (at level 80, ma at level 90, mb at level 200, right associativity) : mutator_scope.
    Notation "ma >>= f" := (scmut_bind ma f) (at level 50, left associativity) : mutator_scope.
    Notation "m1 ;; m2" := (scmut_bind m1 (fun _ => m2)) : mutator_scope.
    Notation "ma *> mb" := (scmut_bind_right ma mb) (at level 50, left associativity) : mutator_scope.
    Notation "ma <* mb" := (scmut_bind_left ma mb) (at level 50, left associativity) : mutator_scope.

  End MutatorNotations.
  Import MutatorNotations.

  Section MutatorOperations.

    Local Open Scope mutator_scope.

    Definition scmut_state {Œì Œì' A} (f : SCState Œì -> (SCState Œì' * A)) : SCMut Œì Œì' A :=
      fun s => outcome_pure (let (s1,a) := f s in MkSCMutResult a s1).
    Definition scmut_modify {Œì Œì'} (f : SCState Œì -> SCState Œì') : SCMut Œì Œì' unit :=
      scmut_state (fun s => (f s,tt)).
    Definition scmut_put {Œì Œì'} (s : SCState Œì') : SCMut Œì Œì' unit :=
      scmut_state (fun _ => (s,tt)).
    Definition scmut_get {Œì} : SCMut Œì Œì (SCState Œì) :=
      scmut_state (fun s => (s,s)).

    Definition scmut_state_local {Œì Œì' A} (f : LocalStore Œì -> (LocalStore Œì' * A)) : SCMut Œì Œì' A :=
      scmut_state (fun '(MkSCState Œ¥ ƒ•) => let (Œ¥',a) := f Œ¥ in (MkSCState Œ¥' ƒ•,a)).
    Definition scmut_modify_local {Œì Œì'} (f : LocalStore Œì -> LocalStore Œì') : SCMut Œì Œì' unit :=
      scmut_state_local (fun Œ¥ => (f Œ¥,tt)).
    Definition scmut_put_local {Œì Œì'} (Œ¥ : LocalStore Œì') : SCMut Œì Œì' unit :=
      scmut_state_local (fun _ => (Œ¥,tt)).
    Definition scmut_get_local {Œì} : SCMut Œì Œì (LocalStore Œì) :=
      scmut_state_local (fun Œ¥ => (Œ¥,Œ¥)).
    Definition scmut_gets_local {Œì A} (f : LocalStore Œì -> A) : SCMut Œì Œì A :=
      scmut_state_local (fun Œ¥ => (Œ¥,f Œ¥)).
    Definition scmut_pop_local {Œì x œÉ} : SCMut (Œì ‚ñª (x , œÉ)) Œì unit :=
      scmut_modify_local (fun Œ¥ => env_tail Œ¥).
    Definition scmut_pops_local {Œì} Œî : SCMut (Œì ‚ñª‚ñª Œî) Œì unit :=
      scmut_modify_local (fun Œ¥ŒìŒî => env_drop Œî Œ¥ŒìŒî).
    Definition scmut_push_local {Œì x œÉ} (v : Lit œÉ) : SCMut Œì (Œì ‚ñª (x , œÉ)) unit :=
      scmut_modify_local (fun Œ¥ => env_snoc Œ¥ (x , œÉ) v).
    Definition scmut_pushs_local {Œì Œî} (Œ¥Œî : LocalStore Œî) : SCMut Œì (Œì ‚ñª‚ñª Œî) unit :=
      scmut_modify_local (fun Œ¥Œì => env_cat Œ¥Œì Œ¥Œî).

    Definition scmut_state_heap {Œì A} (f : SCHeap -> (SCHeap * A)) : SCMut Œì Œì A :=
      scmut_state (fun '(MkSCState Œ¥ h) => let (h',a) := f h in (MkSCState Œ¥ h',a)).
    Definition scmut_modify_heap {Œì} (f : SCHeap -> SCHeap) : SCMut Œì Œì unit :=
      scmut_state_heap (fun h => (f h,tt)).
    Definition scmut_get_heap {Œì} : SCMut Œì Œì SCHeap :=
      scmut_state_heap (fun h => (h,h)).
    Definition scmut_put_heap {Œì} (h : SCHeap) : SCMut Œì Œì unit :=
      scmut_state_heap (fun _ => (h,tt)).

    Definition scmut_eval_exp {Œì œÉ} (e : Exp Œì œÉ) : SCMut Œì Œì (Lit œÉ) :=
      scmut_gets_local (fun Œ¥ => eval e Œ¥).
    Definition scmut_eval_exps {Œì} {œÉs : Ctx (ùëø * Ty)} (es : NamedEnv (Exp Œì) œÉs) : SCMut Œì Œì (LocalStore œÉs) :=
      scmut_gets_local (fun Œ¥ => env_map (fun _ e => eval e Œ¥) es).

    Definition scmut_assert_eq {Œì œÉ} (v1 v2 : Lit œÉ) : SCMut Œì Œì unit :=
      if Lit_eqb œÉ v1 v2
      then scmut_pure tt
      else scmut_fail "Err [smut_assert_eq]: unsatisfiable".

    Definition scmut_produce_chunk {Œì} (c : SCChunk) : SCMut Œì Œì unit :=
      scmut_modify_heap (fun h => c :: h).
    Definition scmut_consume_chunk {Œì} (c : SCChunk) : SCMut Œì Œì unit :=
      scmut_get_heap >>= fun h =>
      scmut_angelic_list
        "Err [scmut_consume_chunk]: empty extraction"
        (extract_chunk_eqb c h) >>= fun h' =>
        scmut_put_heap h'.

    Global Arguments scmut_push_local {Œì _ _} _.
    Global Arguments scmut_produce_chunk {Œì} _.
    Global Arguments scmut_consume_chunk {Œì} _.

    Definition inst_chunk {Œ£} (Œπ : SymInstance Œ£) (c : Chunk Œ£) : SCChunk :=
      match c with
      | chunk_pred p ts  => scchunk_pred p (env_map (fun _ => inst_term Œπ) ts)
      | chunk_ptsreg r t => scchunk_ptsreg r (inst_term Œπ t)
      end.

    Fixpoint scmut_produce {Œì Œ£} (Œπ : SymInstance Œ£) (asn : Assertion Œ£) : SCMut Œì Œì unit :=
      match asn with
      | asn_bool b      => if inst_term Œπ b
                           then scmut_pure tt
                           else scmut_block
      | asn_prop P      => scmut_fail "scmut_produce"
      | asn_chunk c     => scmut_produce_chunk (inst_chunk Œπ c)
      | asn_if b a1 a2  => if inst_term Œπ b
                           then scmut_produce Œπ a1
                           else scmut_produce Œπ a2
      | @asn_match_enum _ E k alts =>
        scmut_produce Œπ (alts (inst_term Œπ k))
      | asn_sep a1 a2   => scmut_produce Œπ a1 *> scmut_produce Œπ a2
      | asn_exist œÇ œÑ a => ‚®Ç v : Lit œÑ => scmut_produce (env_snoc Œπ (œÇ , œÑ) v) a
      end.

    Fixpoint scmut_consume {Œì Œ£} (Œπ : SymInstance Œ£) (asn : Assertion Œ£) : SCMut Œì Œì unit :=
      match asn with
      | asn_bool b      => if inst_term Œπ b
                           then scmut_pure tt
                           else scmut_fail "scmut_consume"
      | asn_prop P      => scmut_fail "scmut_consume"
      | asn_chunk c     => scmut_consume_chunk (inst_chunk Œπ c)
      | asn_if b a1 a2  => if inst_term Œπ b
                           then scmut_consume Œπ a1
                           else scmut_consume Œπ a2
      | @asn_match_enum _ E k alts =>
        scmut_consume Œπ (alts (inst_term Œπ k))
      | asn_sep a1 a2   => scmut_consume Œπ a1 *> scmut_consume Œπ a2
      | asn_exist œÇ œÑ a => ‚®Å v : Lit œÑ => scmut_consume (env_snoc Œπ (œÇ , œÑ) v) a
      end.

    Definition scmut_call {Œì Œî œÑ} (contract : SepContract Œî œÑ) (vs : LocalStore Œî) : SCMut Œì Œì (Lit œÑ) :=
      match contract with
      | @sep_contract_result_pure _ _ Œ£e Œ¥ result req ens =>
        ‚®Å Œπ : SymInstance Œ£e =>
        ‚®Å H : vs = inst_localstore Œπ Œ¥ =>
        scmut_consume Œπ req ;;
        scmut_produce Œπ ens ;;
        scmut_pure (inst_term Œπ result)
      | @sep_contract_result _ _ Œ£e Œ¥ result req ens =>
        ‚®Å Œπ : SymInstance Œ£e =>
        ‚®Å H : vs = inst_localstore Œπ Œ¥ =>
        scmut_consume Œπ req  ;;
        ‚®Å v : Lit œÑ =>
        scmut_produce (env_snoc Œπ (result,œÑ) v) ens ;;
        scmut_pure v
      | sep_contract_none _ _ =>
        scmut_fail "Err [scmut_exec]: call of sep_contract_none"
      end.

    Fixpoint scmut_exec {Œì œÉ} (s : Stm Œì œÉ) : SCMut Œì Œì (Lit œÉ) :=
      match s with
      | stm_lit œÑ l => scmut_pure l
      | stm_exp e => scmut_eval_exp e
      | stm_let x œÑ s k =>
        v <- scmut_exec s ;;
        scmut_push_local v *>
        scmut_exec k       <*
        scmut_pop_local
      | stm_block Œ¥ k =>
        scmut_pushs_local Œ¥ *>
        scmut_exec k <*
        scmut_pops_local _
      | stm_assign x e =>
        v <- scmut_exec e ;;
        scmut_modify_local (fun Œ¥ => Œ¥ ‚ü™ x ‚Ü¶ v ‚ü´)%env *>
        scmut_pure v
      | stm_call f es => scmut_eval_exps es >>= scmut_call (CEnv f)
      | stm_call_external f es => scmut_eval_exps es >>= scmut_call (CEnvEx f)
      | stm_call_frame Œî Œ¥' œÑ s =>
        Œ¥ <- scmut_get_local ;;
        scmut_put_local Œ¥' ;;
        v <- scmut_exec s ;;
        scmut_put_local Œ¥ ;;
        scmut_pure v
      | stm_if e s1 s2 =>
        v <- scmut_eval_exp e ;;
        if v
        then scmut_exec s1
        else scmut_exec s2
      | stm_seq e k => scmut_exec e ;; scmut_exec k
      | stm_assert e1 _ =>
        v <- scmut_eval_exp e1 ;;
        if v
        then scmut_pure v
        else scmut_block
      | stm_fail œÑ s =>
        scmut_block
      | stm_match_enum E e alts =>
        K <- scmut_eval_exp e ;;
        scmut_exec (alts K)
      | @stm_read_register _ œÑ reg =>
        ‚®Å v : Lit œÑ =>
        let c := scchunk_ptsreg reg v in
        scmut_consume_chunk c ;;
        scmut_produce_chunk c ;;
        scmut_pure v
      | @stm_write_register _ œÑ reg e => scmut_eval_exp e >>= fun v__new =>
        ‚®Å v__old : Lit œÑ =>
        scmut_consume_chunk (scchunk_ptsreg reg v__old) ;;
        scmut_produce_chunk (scchunk_ptsreg reg v__new) ;;
        scmut_pure v__new
      | stm_match_list e s1 xh xt s2 =>
        v <- scmut_eval_exp e ;;
        match v with
        | nil => scmut_exec s1
        | cons h t =>
          scmut_push_local h ;;
          scmut_push_local (œÉ := ty_list _) t ;;
          scmut_exec s2 <*
          scmut_pop_local <*
          scmut_pop_local
        end
      | stm_match_sum e xinl s1 xinr s2 =>
        v <- scmut_eval_exp e ;;
        match v with
        | inl v => scmut_push_local v ;; scmut_exec s1 <* scmut_pop_local
        | inr v => scmut_push_local v ;; scmut_exec s2 <* scmut_pop_local
        end
      | stm_match_pair e xl xr s =>
        v <- scmut_eval_exp e ;;
        scmut_push_local (fst v) ;;
        scmut_push_local (snd v) ;;
        scmut_exec s <*
        scmut_pop_local <*
        scmut_pop_local
      | stm_match_tuple e p rhs =>
        v <- scmut_eval_exp e ;;
        scmut_pushs_local (tuple_pattern_match p v) ;;
        scmut_exec rhs <*
        scmut_pops_local _
      | stm_match_union U e alts =>
        v <- scmut_eval_exp e ;;
        let (K , v) := ùëº_unfold v in
        scmut_pushs_local (pattern_match (proj_alt_pat (alts K)) v) ;;
        scmut_exec (proj_alt_rhs (alts K)) <*
        scmut_pops_local _
      | stm_match_record R e p rhs =>
        v <- scmut_eval_exp e ;;
        scmut_pushs_local (record_pattern_match p (ùëπ_unfold v)) ;;
        scmut_exec rhs <*
        scmut_pops_local _
      | stm_bind s k =>
        v <- scmut_exec s ;;
        scmut_exec (k v)
      end.

    Definition scmut_leakcheck {Œì} : SCMut Œì Œì unit :=
      scmut_get_heap >>= fun h =>
      match h with
      | nil => scmut_pure tt
      | _   => scmut_fail "Err [scmut_leakcheck]: heap leak"
      end.

  End MutatorOperations.

  Import OutcomeNotations.

  Definition outcome_contract {Œî : Ctx (ùëø * Ty)} {œÑ : Ty} (c : SepContract Œî œÑ) (s : Stm Œî œÑ) :
    Outcome unit :=
    match c with
    | @sep_contract_result_pure _ _ Œ£ Œ¥ result' req ens =>
      ‚®Ç Œπ : SymInstance Œ£ =>
      let Œ¥Œî : LocalStore Œî := inst_localstore Œπ Œ¥ in
      let mut := (scmut_produce Œπ req ;;
                  scmut_exec s >>= fun result =>
                  scmut_consume Œπ ens ;;
                  scmut_assert_eq result (inst_term Œπ result') ;;
                  scmut_leakcheck)%mut in
      let out := mut (scstate_initial Œ¥Œî) in
      outcome_map (fun _ => tt) out
    | @sep_contract_result _ _ Œ£ Œ¥ result req ens =>
      ‚®Ç Œπ : SymInstance Œ£ =>
      let Œ¥Œî : LocalStore Œî := inst_localstore Œπ Œ¥ in
      let mut := (scmut_produce Œπ req ;;
                  scmut_exec s >>= fun v =>
                  scmut_consume (env_snoc Œπ (result,œÑ) v) ens ;;
                  scmut_leakcheck)%mut in
      let out := mut (scstate_initial Œ¥Œî) in
      outcome_map (fun _ => tt) out
    | @sep_contract_none _ _ =>
      outcome_block
    end.

  Definition ValidContract (Œî : Ctx (ùëø * Ty)) (œÑ : Ty)
             (c : SepContract Œî œÑ) (body : Stm Œî œÑ) : Prop :=
    outcome_satisfy (outcome_contract c body) (fun _ => True).

  Definition ValidContractEnv (cenv : SepContractEnv) : Prop :=
    forall (Œî : Ctx (ùëø * Ty)) (œÑ : Ty) (f : ùë≠ Œî œÑ),
      ValidContract (cenv Œî œÑ f) (Pi f).

End SemiConcrete.
