(******************************************************************************)
(* Copyright (c) 2020 Dominique Devriese, Georgy Lukyanov,                    *)
(*   Sander Huyghebaert, Steven Keuchel                                       *)
(* All rights reserved.                                                       *)
(*                                                                            *)
(* Redistribution and use in source and binary forms, with or without         *)
(* modification, are permitted provided that the following conditions are     *)
(* met:                                                                       *)
(*                                                                            *)
(* 1. Redistributions of source code must retain the above copyright notice,  *)
(*    this list of conditions and the following disclaimer.                   *)
(*                                                                            *)
(* 2. Redistributions in binary form must reproduce the above copyright       *)
(*    notice, this list of conditions and the following disclaimer in the     *)
(*    documentation and/or other materials provided with the distribution.    *)
(*                                                                            *)
(* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        *)
(* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED  *)
(* TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR *)
(* PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR          *)
(* CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,      *)
(* EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,        *)
(* PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR         *)
(* PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF     *)
(* LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING       *)
(* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         *)
(* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               *)
(******************************************************************************)

From Coq Require Import
     Bool.Bool
     Classes.Morphisms
     Classes.RelationClasses
     Lists.List
     (* Program.Basics *)
     (* Program.Tactics *)
     ZArith.

From Katamaran Require Import
     Prelude
     Tactics
     Syntax.Formulas
     Syntax.Predicates
     Syntax.Registers
     Base.

From Equations Require Import
     Equations.

Import ctx.notations.
Import env.notations.

Local Set Implicit Arguments.

Fixpoint heap_extractions `{IsDuplicable C} (h : list C) : list (C * list C) :=
  match h with
  | nil      => nil
  | cons c h => let h' := if is_duplicable c then cons c h else h in
                let ec := pair c h' in
                cons ec (map (base.prod_map id (cons c)) (heap_extractions h))
  end.

Lemma heap_extractions_map `{IsDuplicable A} `{IsDuplicable B} (f : A -> B) (h : list A)
      (is_dup_map : (forall c, is_duplicable (f c) = is_duplicable c)) :
  heap_extractions (List.map f h) = List.map (base.prod_map f (List.map f)) (heap_extractions h).
Proof.
  induction h; cbn.
  - reflexivity.
  - f_equal.
    + unfold base.prod_map; cbn. f_equal.
      rewrite is_dup_map.
      now destruct is_duplicable.
    + rewrite IHh.
      rewrite ?List.map_map.
      apply List.map_ext.
      intros [x xs]; reflexivity.
Qed.

Module Type ChunksOn
  (Import B : Base)
  (Import P : PredicateKit B)
  (Import F : FormulasOn B P).

  Inductive GChunk (V : Ty -> Set) : Type :=
  | chunk_user   (p : ð‘¯) (ts : Env V (ð‘¯_Ty p))
  | chunk_ptsreg {Ïƒ : Ty} (r : ð‘¹ð‘¬ð‘® Ïƒ) (v : V Ïƒ)
  | chunk_conj   (c1 c2 : GChunk V)
  | chunk_wand   (c1 c2 : GChunk V).
  Global Arguments chunk_user [_] _ _.
  Global Arguments chunk_ptsreg [_] [_] _ _.

  (* Semi-concrete chunks *)
  Definition SCChunk := GChunk Val.

  (* Symbolic chunks *)
  Definition Chunk (Î£ : LCtx) := GChunk (Term Î£).

  Section TransparentObligations.
    Local Set Transparent Obligations.
    Derive NoConfusion for GChunk.
  End TransparentObligations.

  #[export] Instance chunk_isdup {V} : IsDuplicable (GChunk V) := {
    is_duplicable := fun c => match c with
                           | chunk_user p _ => is_duplicable p
                           | chunk_ptsreg _ _ => false
                           | chunk_conj _ _ => false
                           | chunk_wand _ _ => false
                           end
    }.

  Open Scope lazy_bool_scope.

  Fixpoint chunk_eqb {V : Ty -> Set} (eqv : forall Ïƒ, V Ïƒ -> V Ïƒ -> bool) (c1 c2 : GChunk V) : bool :=
    match c1 , c2 with
    | chunk_user p1 ts1, chunk_user p2 ts2 =>
      match eq_dec p1 p2 with
      | left e => env.eqb_hom
                    eqv
                    (eq_rect _ (fun p => Env _ (ð‘¯_Ty p)) ts1 _ e)
                    ts2
      | right _ => false
      end
    | chunk_ptsreg r1 t1 , chunk_ptsreg r2 t2 =>
      match eq_dec_het r1 r2 with
      | left e  => eqv _
                     (eq_rect _ V t1 _ (f_equal projT1 e))
                     t2
      | right _ => false
      end
    | chunk_conj c11 c12 , chunk_conj c21 c22 =>
      chunk_eqb eqv c11 c21 &&& chunk_eqb eqv c12 c22
    | chunk_wand c11 c12 , chunk_wand c21 c22 =>
      chunk_eqb eqv c11 c21 &&& chunk_eqb eqv c12 c22
    | _ , _ => false
    end.

  Local Set Equations With UIP.

  Lemma chunk_eqb_spec {V : Ty -> Set} eqv (eqv_spec : forall {Ïƒ} {v1 v2 : V Ïƒ}, reflect (v1 = v2) (eqv _ v1 v2)):
    forall (c1 c2 : GChunk V),
      reflect (c1 = c2) (chunk_eqb eqv c1 c2).
  Proof.
    induction c1; intros [];
      solve_eqb_spec with
      try match goal with
          | |- reflect _ (env.eqb_hom eqv ?x ?y) =>
              destruct (env.eqb_hom_spec eqv eqv_spec x y)
          | |- reflect _ (eqv _ ?x ?y) =>
              destruct (eqv_spec _ x y)
          end.
  Qed.

  #[export] Instance SubstChunk : Subst Chunk :=
    fix sub_chunk {Î£1} (c : Chunk Î£1) {Î£2} (Î¶ : Sub Î£1 Î£2) {struct c} : Chunk Î£2 :=
      match c with
      | chunk_user p ts => chunk_user p (subst ts Î¶)
      | chunk_ptsreg r t => chunk_ptsreg r (subst t Î¶)
      | chunk_conj c1 c2 =>
        chunk_conj (sub_chunk c1 Î¶) (sub_chunk c2 Î¶)
      | chunk_wand c1 c2 =>
        chunk_wand (sub_chunk c1 Î¶) (sub_chunk c2 Î¶)
      end.

  #[export] Instance substlaws_chunk : SubstLaws Chunk.
  Proof.
    constructor.
    { intros ? c. induction c; cbn; f_equal; auto; apply subst_sub_id. }
    { intros ? ? ? ? ? c. induction c; cbn; f_equal; auto; apply subst_sub_comp. }
  Qed.

  #[export] Instance inst_chunk : Inst Chunk SCChunk :=
    fix inst_chunk {Î£} (c : Chunk Î£) (Î¹ : Valuation Î£) {struct c} : SCChunk :=
    match c with
    | chunk_user p ts => chunk_user p (inst ts Î¹)
    | chunk_ptsreg r t => chunk_ptsreg r (inst t Î¹)
    | chunk_conj c1 c2 => chunk_conj (inst_chunk c1 Î¹) (inst_chunk c2 Î¹)
    | chunk_wand c1 c2 => chunk_wand (inst_chunk c1 Î¹) (inst_chunk c2 Î¹)
    end.

  #[export] Instance inst_subst_chunk : InstSubst Chunk SCChunk.
  Proof.
    intros ? ? Î¶ Î¹ c; induction c; cbn; f_equal; auto; apply inst_subst.
  Qed.

  Import option.notations.
  #[export] Instance OccursCheckChunk :
    OccursCheck Chunk :=
    fun Î£ b bIn =>
      fix occurs_check_chunk (c : Chunk Î£) : option (Chunk (Î£ - b)) :=
      match c with
      | chunk_user p ts => option.map (chunk_user p) (occurs_check bIn ts)
      | chunk_ptsreg r t => option.map (chunk_ptsreg r) (occurs_check bIn t)
      | chunk_conj c1 c2 =>
          c1' <- occurs_check_chunk c1 ;;
          c2' <- occurs_check_chunk c2 ;;
          Some (chunk_conj c1' c2')
      | chunk_wand c1 c2 =>
          c1' <- occurs_check_chunk c1 ;;
          c2' <- occurs_check_chunk c2 ;;
          Some (chunk_wand c1' c2')
      end.

  Definition SCHeap : Type := list SCChunk.
  Definition SHeap : LCtx -> Type := fun Î£ => list (Chunk Î£).

  #[export] Instance inst_heap : Inst SHeap SCHeap :=
    inst_list.
  #[export] Instance inst_subst_heap : InstSubst SHeap SCHeap.
  Proof. apply inst_subst_list. Qed.

  Fixpoint peval_chunk {Î£} (c : Chunk Î£) : Chunk Î£ :=
    match c with
    | chunk_user p ts => chunk_user p (env.map peval ts)
    | chunk_ptsreg r t => chunk_ptsreg r (peval t)
    | chunk_conj c1 c2 => chunk_conj (peval_chunk c1) (peval_chunk c2)
    | chunk_wand c1 c2 => chunk_wand (peval_chunk c1) (peval_chunk c2)
    end.

  Lemma peval_chunk_sound {Î£} (c : Chunk Î£) :
    forall (Î¹ : Valuation Î£),
      inst (peval_chunk c) Î¹ = inst c Î¹.
  Proof.
    induction c; cbn; intros Î¹; f_equal; auto using peval_sound.
    apply pevals_sound. apply peval_sound.
  Qed.

  Lemma inst_is_duplicable {Î£} (c : Chunk Î£) (Î¹ : Valuation Î£) :
    is_duplicable (inst c Î¹) = is_duplicable c.
  Proof.
    destruct c; now cbn.
  Qed.

  Section Consume.
    Import EqNotations.

    Fixpoint try_consume_chunk_exact {Î£} (h : SHeap Î£) (c : Chunk Î£) : option (SHeap Î£) :=
      match h with
      | nil       => None
      | cons c' h =>
          if chunk_eqb Term_eqb c c'
          then Some (if is_duplicable c then (cons c h) else h)
          else option_map (cons c') (try_consume_chunk_exact h c)
      end.

    Lemma try_consume_chunk_exact_spec {Î£} (h : SHeap Î£) (c : Chunk Î£) :
      option.wlp
        (fun h' => List.In (c , h') (heap_extractions h))
        (try_consume_chunk_exact h c).
    Proof.
      induction h as [|c' h].
      - now constructor.
      - cbn -[is_duplicable].
        destruct (chunk_eqb_spec _ (@Term_eqb_spec _) c c').
        + constructor. left. subst.
          remember (is_duplicable c') as dup.
          destruct dup; reflexivity.
        + apply option.wlp_map. revert IHh.
          apply option.wlp_monotonic; auto.
          intros h' HIn. right.
          rewrite List.in_map_iff.
          exists (c,h'). auto.
    Qed.

    Section PreciseUser.

      Context {Î£} (p : ð‘¯) {Î”I Î”O : Ctx Ty} (prec : ð‘¯_Ty p = Î”I â–»â–» Î”O)
        (tsI : Env (Term Î£) Î”I) (tsO : Env (Term Î£) Î”O).

      Equations(noeqns) match_chunk_user_precise (c : Chunk Î£) : option (PathCondition Î£) :=
        match_chunk_user_precise (chunk_user p' ts')
          with eq_dec p p' => {
            match_chunk_user_precise (chunk_user ?(p) ts') (left eq_refl) :=
              match env.catView (rew prec in ts') with
              | env.isCat tsI' tsO' =>
                  if env.eqb_hom Term_eqb tsI tsI'
                  then Some (formula_eqs_ctx tsO tsO')
                  else None
              end;
            match_chunk_user_precise (chunk_user p' ts') (right _) := None
          };
        match_chunk_user_precise _ := None.

      Fixpoint try_consume_chunk_user_precise (h : SHeap Î£) : option (SHeap Î£ * PathCondition Î£) :=
        match h with
        | nil => None
        | cons c h' =>
            match match_chunk_user_precise c with
            | Some eqs => Some (if is_duplicable p then cons c h' else h', eqs)
            | None => option_map (base.prod_map (cons c) id) (try_consume_chunk_user_precise h')
            end
        end.

      Lemma try_consume_chunk_user_precise_spec (h : SHeap Î£) :
        option.wlp
          (fun '(h', eqs) =>
             forall Î¹ : Valuation Î£,
             instprop eqs Î¹ ->
             List.In
               (inst (chunk_user p (eq_rect_r (fun c : Ctx Ty => Env (Term Î£) c) (tsI â–ºâ–º tsO) prec)) Î¹, inst h' Î¹)
               (heap_extractions (inst h Î¹)))
          (try_consume_chunk_user_precise h).
      Proof.
        induction h as [|c h]; [now constructor|]. cbn [try_consume_chunk_user_precise].
        destruct match_chunk_user_precise as [eqs|] eqn:?.
        - clear IHh. constructor. intros Î¹ Heqs. left.
          destruct c; try discriminate Heqo. cbn in *.
          destruct (eq_dec p p0); cbn in Heqo; try discriminate Heqo. destruct e.
          remember (eq_rect (ð‘¯_Ty p) (Env (Term Î£)) ts (Î”I â–»â–» Î”O) prec) as ts'.
          destruct (env.catView ts') as [tsI' tsO'].
          destruct (env.eqb_hom_spec Term_eqb (@Term_eqb_spec Î£) tsI tsI'); try discriminate.
          apply noConfusion_inv in Heqo. cbn in Heqo. subst.
          apply instprop_formula_eqs_ctx in Heqs.
          rewrite (@inst_eq_rect_indexed_r (Ctx Ty) (fun Î” Î£ => Env (Term Î£) Î”) (Env Val)).
          rewrite inst_env_cat. rewrite Heqs. rewrite <- inst_env_cat.
          change (env.cat ?A ?B) with (env.cat A B). rewrite Heqts'.
          rewrite (@inst_eq_rect_indexed (Ctx Ty) (fun Î” Î£ => Env (Term Î£) Î”) (Env Val)).
          rewrite rew_opp_l. now destruct is_duplicable.
        - apply option.wlp_map. revert IHh. apply option.wlp_monotonic; auto.
          intros [h' eqs] HYP Î¹ Heqs. specialize (HYP Î¹ Heqs).
          remember (inst (chunk_user p (eq_rect_r (fun c0 : Ctx Ty => Env (Term Î£) c0) (tsI â–ºâ–º tsO) prec)) Î¹) as c'.
          change (inst (cons c h) Î¹) with (cons (inst c Î¹) (inst h Î¹)).
          cbn [fst heap_extractions]. right. apply List.in_map_iff.
          eexists (c', inst h' Î¹); auto.
      Qed.

    End PreciseUser.

    Section PrecisePtsreg.

      Context {Î£ : LCtx} {Ïƒ} (r : ð‘¹ð‘¬ð‘® Ïƒ).

      Equations(noeqns) match_chunk_ptsreg_precise (c : Chunk Î£) : option (Term Î£ Ïƒ) :=
        match_chunk_ptsreg_precise (chunk_ptsreg r' t)
          with eq_dec_het r r' => {
            match_chunk_ptsreg_precise (chunk_ptsreg ?(r) t) (left eq_refl) :=
              Some t;
            match_chunk_ptsreg_precise (chunk_ptsreg r' t) (right _) := None
          };
        match_chunk_ptsreg_precise _ := None.

      Fixpoint find_chunk_ptsreg_precise (h : SHeap Î£) : option (Term Î£ Ïƒ * SHeap Î£) :=
        match h with
        | nil => None
        | cons c h' =>
            match match_chunk_ptsreg_precise c with
            | Some t => Some (t, h')
            | None => option_map (base.prod_map id (cons c)) (find_chunk_ptsreg_precise h')
            end
        end.

      Lemma find_chunk_ptsreg_precise_spec (h : SHeap Î£) :
        option.wlp
          (fun '(t, h') =>
             forall Î¹ : Valuation Î£,
             List.In
               (inst (chunk_ptsreg r t) Î¹, inst h' Î¹)
               (heap_extractions (inst h Î¹)))
          (find_chunk_ptsreg_precise h).
      Proof.
        induction h as [|c h]; [now constructor|]; cbn [find_chunk_ptsreg_precise].
        destruct match_chunk_ptsreg_precise eqn:?.
        - constructor. intros Î¹. clear IHh.
          destruct c; cbn in Heqo; try discriminate Heqo.
          destruct (eq_dec_het r r0); try discriminate Heqo.
          dependent elimination e. cbn in Heqo. dependent elimination Heqo.
          change (inst (cons ?c ?h) Î¹) with (cons (inst c Î¹) (inst h Î¹)).
          cbn. now left.
        - apply option.wlp_map. revert IHh. apply option.wlp_monotonic; auto.
          intros [t h'] HYP Î¹. specialize (HYP Î¹).
          change (inst (cons ?c ?h) Î¹) with (cons (inst c Î¹) (inst h Î¹)).
          cbn [fst heap_extractions]. right. apply List.in_map_iff.
          eexists (inst (T := Chunk) (chunk_ptsreg (V := Term Î£) r t) Î¹, inst h' Î¹).
          split; auto.
      Qed.

      Context (h : SHeap Î£) (t : Term Î£ Ïƒ).

      Definition try_consume_chunk_ptsreg_precise :
        option (SHeap Î£ * PathCondition Î£) :=
        option.map
          (fun '(t', h') => (h', ctx.nil â–» formula_relop bop.eq t t'))
          (find_chunk_ptsreg_precise h).

      Lemma try_consume_chunk_ptsreg_precise_spec :
        option.wlp
          (fun '(h', eqs) =>
             forall Î¹ : Valuation Î£,
             instprop eqs Î¹ ->
             List.In
               (inst (chunk_ptsreg r t) Î¹, inst h' Î¹)
               (heap_extractions (inst h Î¹)))
          try_consume_chunk_ptsreg_precise.
      Proof.
        unfold try_consume_chunk_ptsreg_precise. apply option.wlp_map.
        generalize (find_chunk_ptsreg_precise_spec h).
        apply option.wlp_monotonic. intros [h' t'] HIn Î¹ [_ Heq].
        specialize (HIn Î¹). cbn in Heq |- *. now rewrite Heq.
      Qed.

    End PrecisePtsreg.

    Definition try_consume_chunk_precise {Î£} (h : SHeap Î£) (c : Chunk Î£) :
      option (SHeap Î£ * PathCondition Î£) :=
      match c with
      | chunk_user p ts =>
          match ð‘¯_precise p with
          | Some (MkPrecise Î”I Î”O Î”eq) =>
              match env.catView (rew Î”eq in ts) with
              | env.isCat tsI tsO => try_consume_chunk_user_precise Î”eq tsI tsO h
              end
          | None => None
          end
      | chunk_ptsreg r t => try_consume_chunk_ptsreg_precise r h t
      | _ => None
      end.

    Lemma try_consume_chunk_precise_spec {Î£} (h : SHeap Î£) (c : Chunk Î£) :
      option.wlp
        (fun '(h', eqs) =>
           forall Î¹ : Valuation Î£,
           instprop eqs Î¹ ->
           List.In (inst c Î¹, inst h' Î¹)
             (heap_extractions (inst h Î¹)))
        (try_consume_chunk_precise h c).
    Proof.
      destruct c; [| |constructor|constructor];
        cbn [try_consume_chunk_precise].
      - destruct (ð‘¯_precise p) as [[Î”I Î”O prec]|]; [|constructor].
        remember (eq_rect (ð‘¯_Ty p) (Env (Term Î£)) ts (Î”I â–»â–» Î”O) prec) as ts'.
        destruct (env.catView ts') as [tsI tsO].
        generalize (try_consume_chunk_user_precise_spec prec tsI tsO h).
        apply option.wlp_monotonic. intros [h' eqs].
        intros HIn Î¹ Heqs. specialize (HIn Î¹ Heqs).
        now rewrite Heqts', rew_opp_l in HIn.
      - apply try_consume_chunk_ptsreg_precise_spec.
    Qed.

  End Consume.

  Section Interpretation.
    Import iris.bi.interface.

    Context {HProp} `{PI : PredicateDef HProp}.

    Fixpoint interpret_chunk {Î£} (c : Chunk Î£) (Î¹ : Valuation Î£) {struct c} : HProp :=
      match c with
      | chunk_user p ts => luser p (inst ts Î¹)
      | chunk_ptsreg r t => lptsreg r (inst t Î¹)
      | chunk_conj c1 c2 => interpret_chunk c1 Î¹ âˆ— interpret_chunk c2 Î¹
      | chunk_wand c1 c2 => interpret_chunk c1 Î¹ -âˆ— interpret_chunk c2 Î¹
      end.

    Fixpoint interpret_scchunk (c : SCChunk) : HProp :=
      match c with
      | chunk_user p vs => luser p vs
      | chunk_ptsreg r v => lptsreg r v
      | chunk_conj c1 c2 => interpret_scchunk c1 âˆ— interpret_scchunk c2
      | chunk_wand c1 c2 => interpret_scchunk c1 -âˆ— interpret_scchunk c2
      end.

    Definition interpret_scheap : SCHeap -> HProp :=
      List.fold_right (fun c h => interpret_scchunk c âˆ— h)%I emp%I.
    #[global] Arguments interpret_scheap !h.

    Lemma interpret_scchunk_inst {Î£} (c : Chunk Î£) (Î¹ : Valuation Î£) :
      interpret_scchunk (inst c Î¹) = interpret_chunk c Î¹.
    Proof.
      induction c; cbn [interpret_chunk];
        try rewrite <- IHc1, <- IHc2; reflexivity.
    Qed.

  End Interpretation.

End ChunksOn.
