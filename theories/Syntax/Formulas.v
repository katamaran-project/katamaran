(******************************************************************************)
(* Copyright (c) 2020 Dominique Devriese, Georgy Lukyanov,                    *)
(*   Sander Huyghebaert, Steven Keuchel                                       *)
(* All rights reserved.                                                       *)
(*                                                                            *)
(* Redistribution and use in source and binary forms, with or without         *)
(* modification, are permitted provided that the following conditions are     *)
(* met:                                                                       *)
(*                                                                            *)
(* 1. Redistributions of source code must retain the above copyright notice,  *)
(*    this list of conditions and the following disclaimer.                   *)
(*                                                                            *)
(* 2. Redistributions in binary form must reproduce the above copyright       *)
(*    notice, this list of conditions and the following disclaimer in the     *)
(*    documentation and/or other materials provided with the distribution.    *)
(*                                                                            *)
(* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        *)
(* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED  *)
(* TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR *)
(* PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR          *)
(* CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,      *)
(* EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,        *)
(* PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR         *)
(* PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF     *)
(* LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING       *)
(* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         *)
(* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               *)
(******************************************************************************)

From Coq Require Import
     Bool.Bool
     Classes.Morphisms
     Classes.Morphisms_Prop
     Classes.RelationClasses
     Program.Basics
     Program.Tactics
     Relations.Relation_Definitions
     ZArith.

From Katamaran Require Import
     Prelude
     Notations
     Syntax.Predicates
     Base.

From Equations Require Import
     Equations.

Import ctx.notations.
Import env.notations.

Local Set Implicit Arguments.

Module Type FormulasOn
  (Import B : Base)
  (Import P : PredicateKit B).

  Local Obligation Tactic := idtac.

  Inductive Formula (Î£ : LCtx) : Type :=
  | formula_user (p : ð‘·) (ts : Env (Term Î£) (ð‘·_Ty p))
  | formula_bool (t : Term Î£ ty.bool)
  | formula_prop {Î£'} (Î¶ : Sub Î£' Î£) (P : abstract_named Val Î£' Prop)
  | formula_relop {Ïƒ} (rop : bop.RelOp Ïƒ) (t1 t2 : Term Î£ Ïƒ)
  | formula_true
  | formula_false
  | formula_and (F1 F2 : Formula Î£)
  | formula_or (F1 F2 : Formula Î£).
  #[global] Arguments formula_user {_} p ts.
  #[global] Arguments formula_bool {_} t.
  #[global] Arguments formula_true {_}.
  #[global] Arguments formula_false {_}.

  Definition formula_relop_neg {Î£ Ïƒ} (op : RelOp Ïƒ) :
    forall (t1 t2 : Term Î£ Ïƒ), Formula Î£ :=
    match op with
    | bop.eq     => formula_relop bop.neq
    | bop.neq    => formula_relop bop.eq
    | bop.le     => Basics.flip (formula_relop bop.lt)
    | bop.lt     => Basics.flip (formula_relop bop.le)
    | bop.bvsle  => Basics.flip (formula_relop bop.bvslt)
    | bop.bvslt  => Basics.flip (formula_relop bop.bvsle)
    | bop.bvule  => Basics.flip (formula_relop bop.bvult)
    | bop.bvult  => Basics.flip (formula_relop bop.bvule)
    end.

  #[export] Instance sub_formula : Subst Formula :=
    fix sub_formula {Î£} fml {Î£2} Î¶ {struct fml} :=
      match fml with
      | formula_user p ts      => formula_user p (subst ts Î¶)
      | formula_bool t         => formula_bool (subst t Î¶)
      | formula_prop Î¶' P      => formula_prop (subst Î¶' Î¶) P
      | formula_relop op t1 t2 => formula_relop op (subst t1 Î¶) (subst t2 Î¶)
      | formula_true           => formula_true
      | formula_false          => formula_false
      | formula_and F1 F2      => formula_and (sub_formula F1 Î¶) (sub_formula F2 Î¶)
      | formula_or F1 F2       => formula_or (sub_formula F1 Î¶) (sub_formula F2 Î¶)
      end.

  #[export] Instance substlaws_formula : SubstLaws Formula.
  Proof.
      constructor.
      { intros ? F.
        induction F; cbn; f_equal; auto; apply subst_sub_id.
      }
      { intros ? ? ? ? ? F.
        induction F; cbn; f_equal; auto; apply subst_sub_comp.
      }
  Qed.

  #[export] Instance instprop_formula : InstProp Formula :=
    fix inst_formula {Î£} (fml : Formula Î£) (Î¹ : Valuation Î£) :=
      match fml with
      | formula_user p ts      => env.uncurry (ð‘·_inst p) (inst ts Î¹)
      | formula_bool t         => inst (A := Val ty.bool) t Î¹ = true
      | formula_prop Î¶ P       => uncurry_named P (inst Î¶ Î¹)
      | formula_relop op t1 t2 => bop.eval_relop_prop op (inst t1 Î¹) (inst t2 Î¹)
      | formula_true           => True
      | formula_false          => False
      | formula_and F1 F2      => inst_formula F1 Î¹ /\ inst_formula F2 Î¹
      | formula_or F1 F2       => inst_formula F1 Î¹ \/ inst_formula F2 Î¹
      end.

  #[export] Instance instprop_subst_formula : InstPropSubst Formula.
  Proof.
    intros ? ? ? ? f. induction f; cbn; rewrite ?inst_subst; auto.
    now apply and_iff_morphism. now apply or_iff_morphism.
  Qed.

  Lemma instprop_formula_relop_neg {Î£ Ïƒ} (Î¹ : Valuation Î£) (op : RelOp Ïƒ) :
    forall (t1 t2 : Term Î£ Ïƒ),
      instprop (formula_relop_neg op t1 t2) Î¹ <->
      bop.eval_relop_val op (inst t1 Î¹) (inst t2 Î¹) = false.
  Proof.
    destruct op; cbn; intros t1 t2;
      unfold bv.sle, bv.sleb, bv.slt, bv.sltb;
      unfold bv.ule, bv.uleb, bv.ult, bv.ultb;
      rewrite ?N.ltb_antisym, ?negb_true_iff, ?negb_false_iff, ?N.leb_gt, ?N.leb_le;
      auto; try Lia.lia; now destruct eq_dec.
  Qed.

  Section Reasoning.
    Import Entailment.

    #[export] Instance proper_formula_user [Î£ p] :
      Proper (base.equiv ==> (âŠ£âŠ¢)) (@formula_user Î£ p).
    Proof. intros xs ys xys Î¹; cbn; now rewrite xys. Qed.

    #[export] Instance proper_formula_bool [Î£] :
      Proper (base.equiv ==> (âŠ£âŠ¢)) (@formula_bool Î£).
    Proof. intros s t e Î¹; cbn; now rewrite e. Qed.

    #[export] Instance proper_formula_relop [Î£ Ïƒ] (rop : RelOp Ïƒ) :
      Proper (base.equiv ==> base.equiv ==> (âŠ£âŠ¢)) (@formula_relop Î£ Ïƒ rop).
    Proof. intros s1 t1 e1 s2 t2 e2 Î¹; cbn; now rewrite e1, e2. Qed.

    Lemma formula_bool_and [Î£] (t1 t2 : Term Î£ ty.bool):
      formula_bool (term_binop bop.and t1 t2) âŠ£âŠ¢ formula_and (formula_bool t1) (formula_bool t2).
    Proof. intro Î¹. cbn. rewrite andb_true_iff. intuition. Qed.
    #[local] Hint Rewrite formula_bool_and : katamaran.

    Lemma formula_bool_relop [Î£ Ïƒ] (op : RelOp Ïƒ) (s t : Term Î£ Ïƒ) :
      formula_bool (term_binop (bop.relop op) s t) âŠ£âŠ¢ formula_relop op s t.
    Proof. intro; cbn; symmetry; apply bop.eval_relop_equiv. Qed.

    Lemma formula_bool_relop_neg [Î£ Ïƒ] (op : RelOp Ïƒ) (s t : Term Î£ Ïƒ) :
      formula_bool (term_relop_neg op s t) âŠ£âŠ¢ formula_relop_neg op s t.
    Proof.
      intro; cbn.
      rewrite inst_term_relop_neg, negb_true_iff.
      now rewrite instprop_formula_relop_neg.
    Qed.

    Lemma formula_relop_val [Î£ Ïƒ] (op : RelOp Ïƒ) (v1 v2 : Val Ïƒ) :
      formula_relop (Î£:=Î£) op (term_val Ïƒ v1) (term_val Ïƒ v2) âŠ£âŠ¢
      if bop.eval_relop_val op v1 v2 then formula_true else formula_false.
    Proof.
      intro. cbn. rewrite bop.eval_relop_equiv.
      now destruct bop.eval_relop_val.
    Qed.

    Lemma formula_and_l [Î£] (F1 F2 : Formula Î£) : formula_and F1 F2 âŠ¢ F1.
    Proof. intros Î¹ H. apply H. Qed.

    Lemma formula_and_r [Î£] (F1 F2 : Formula Î£) : formula_and F1 F2 âŠ¢ F2.
    Proof. intros Î¹ H. apply H. Qed.

    Lemma unsatisfiable_formula_bool [Î£] (t : Term Î£ ty.bool) :
      base.equiv t (term_val ty.bool false) -> Unsatisfiable (formula_bool t).
    Proof. intros e Î¹. specialize (e Î¹). cbn in *. congruence. Qed.

    Lemma unsatisfiable_formula_false [Î£] :
      Unsatisfiable (@formula_false Î£).
    Proof. unfold Unsatisfiable; intuition. Qed.

  End Reasoning.

  Import option.notations.
  #[export] Instance OccursCheckFormula : OccursCheck Formula :=
    fix oc {Î£ x} xIn fml {struct fml} :=
      match fml with
      | formula_user p ts      => option.map (formula_user p) (occurs_check xIn ts)
      | formula_bool t         => option.map formula_bool (occurs_check xIn t)
      | formula_prop Î¶ P       => option.map (fun Î¶' => formula_prop Î¶' P) (occurs_check xIn Î¶)
      | formula_relop op t1 t2 => t1' <- occurs_check xIn t1 ;;
                                  t2' <- occurs_check xIn t2 ;;
                                  Some (formula_relop op t1' t2')
      | formula_true           => Some formula_true
      | formula_false          => Some formula_false
      | formula_and F1 F2      => F1' <- oc xIn F1 ;;
                                  F2' <- oc xIn F2 ;;
                                  Some (formula_and F1' F2')
      | formula_or F1 F2       => F1' <- oc xIn F1 ;;
                                  F2' <- oc xIn F2 ;;
                                  Some (formula_or F1' F2')
      end.

  #[export] Instance occurs_check_laws_formula : OccursCheckLaws Formula.
  Proof. occurs_check_derive. Qed.

  Section PathCondition.
    Import Entailment.

    Definition PathCondition (Î£ : LCtx) : Type := Ctx (Formula Î£).

    Lemma formula_cons_true [Î£] (k : PathCondition Î£) :
      k â–» formula_true âŠ£âŠ¢ k.
    Proof. symmetry. now apply snoc_cancel. Qed.

    Lemma formula_snoc_and [Î£] (k : PathCondition Î£) (F1 F2 : Formula Î£) :
      k â–» formula_and F1 F2 âŠ£âŠ¢ k â–» F1 â–» F2.
    Proof. intro Î¹. cbn. intuition. Qed.

    Equations(noeqns) formula_eqs_ctx {Î” : Ctx Ty} {Î£ : LCtx}
      (Î´ Î´' : Env (Term Î£) Î”) : PathCondition Î£ :=
    | env.nil,        env.nil          => ctx.nil
    | env.snoc Î´ _ t, env.snoc Î´' _ t' =>
      ctx.snoc (formula_eqs_ctx Î´ Î´') (formula_relop bop.eq t t').

    Equations(noeqns) formula_eqs_nctx {N : Set} {Î” : NCtx N Ty} {Î£ : LCtx}
      (Î´ Î´' : NamedEnv (Term Î£) Î”) : PathCondition Î£ :=
    | env.nil,        env.nil          => ctx.nil
    | env.snoc Î´ _ t, env.snoc Î´' _ t' =>
      ctx.snoc (formula_eqs_nctx Î´ Î´') (formula_relop bop.eq t t').

    Lemma instprop_formula_eqs_ctx {Î” Î£} (xs ys : Env (Term Î£) Î”) Î¹ :
      instprop (formula_eqs_ctx xs ys) Î¹ <-> inst xs Î¹ = inst ys Î¹.
    Proof.
      induction xs; env.destroy ys; cbn; [easy|].
      now rewrite IHxs, env.inversion_eq_snoc.
    Qed.

    Lemma instprop_formula_eqs_nctx {N : Set} {Î” : NCtx N Ty} {Î£} (xs ys : NamedEnv (Term Î£) Î”) Î¹ :
      instprop (formula_eqs_nctx xs ys) Î¹ <-> inst xs Î¹ = inst ys Î¹.
    Proof.
      induction xs; env.destroy ys; cbn; [easy|].
      now rewrite IHxs, env.inversion_eq_snoc.
    Qed.

  End PathCondition.
  Bind Scope ctx_scope with PathCondition.

  Module Import DList.
    Import Entailment.
    Record DList (Î£ : LCtx) : Type :=
      MkDList
      { raw : PathCondition Î£ -> Option PathCondition Î£;
        wf : forall k Î¹, instprop (raw k) Î¹ <-> instprop (raw ctx.nil) Î¹ /\ instprop k Î¹;
      }.

    #[export] Instance instprop_dlist : InstProp DList :=
      fun Î£ x Î¹ => instprop (raw x [ctx]) Î¹.

    Section Alternative.
      Let equiv {Î£} : relation (DList Î£) :=
        fun x y =>
          forall k1 k2 : PathCondition Î£,
            k1 âŠ£âŠ¢ k2 -> raw x k1 âŠ£âŠ¢ raw y k2.

      Goal forall {Î£} (x y : DList Î£),
        equiv x y <-> (x âŠ£âŠ¢ y).
      Proof.
        intros Î£ x y.
        change (equiv x y <-> (raw x [ctx] âŠ£âŠ¢ raw y [ctx])).
        destruct x as [x mx], y as [y my]; unfold equiv; cbn.
        split; intros HYP.
        - now apply HYP.
        - intros k1 k2 Hk Î¹. specialize (Hk Î¹). specialize (HYP Î¹).
          rewrite mx, my. intuition.
      Qed.
    End Alternative.

    Definition singleton {Î£} (F : Formula Î£) : DList Î£.
      refine (MkDList (fun k => Some (k â–» F)) _).
      abstract (cbn; intuition).
    Defined.
    Definition error {Î£} : DList Î£.
    Proof.
      refine (MkDList (fun k => None) _).
      abstract (cbn; intuition).
    Defined.
    Definition empty {Î£} : DList Î£.
      refine (MkDList Some _).
      abstract (cbn; intuition).
    Defined.
    Definition cat {Î£} (xs ys : DList Î£) : DList Î£.
      refine (MkDList (fun k => option.bind (raw xs k) (raw ys)) _).
      abstract
        (destruct xs as [rx wx], ys as [ry wy]; cbn; intros k Î¹;
         specialize (wx k Î¹); destruct (rx k) as [k1|], (rx ctx.nil) as [k2|];
         cbn in *; try rewrite (wy k1); try rewrite (wy k2); intuition).
    Defined.
    #[local] Arguments cat {Î£} !_ !_ /.

    Lemma instprop_dlist_singleton [Î£] (F : Formula Î£) (Î¹ : Valuation Î£) :
      instprop (singleton F) Î¹ <-> instprop F Î¹.
    Proof. now cbn. Qed.

    Lemma instprop_dlist_cat [Î£] (x y : DList Î£) (Î¹ : Valuation Î£) :
      instprop (cat x y) Î¹ <-> instprop x Î¹ /\ instprop y Î¹.
    Proof.
      destruct x as [x wx], y as [y wy]; cbn.
      destruct (x [ctx]); cbn; [|easy].
      rewrite wy. intuition.
    Qed.

    #[global] Arguments singleton : simpl never.
    #[global] Arguments cat : simpl never.

    Definition run [Î£] (xs : DList Î£) : Option PathCondition Î£ :=
      raw xs ctx.nil.

    Lemma run_singleton {Î£} (F : Formula Î£) :
      run (singleton F) âŠ£âŠ¢ Some [F]%ctx.
    Proof. easy. Qed.

    #[export] Instance proper_singleton [Î£] : Proper ((âŠ£âŠ¢) ==> (âŠ£âŠ¢)) (@DList.singleton Î£).
    Proof. intros F1 F2 HF Î¹. apply and_iff_morphism; auto. Qed.

    #[export] Instance proper_cat [Î£] : Proper ((âŠ£âŠ¢) ==> (âŠ£âŠ¢) ==> (âŠ£âŠ¢)) (@DList.cat Î£).
    Proof. repeat intro. rewrite !instprop_dlist_cat. now apply and_iff_morphism. Qed.

    Lemma empty_l_valid [Î£] (xs : DList Î£) : Valid xs -> empty âŠ£âŠ¢ xs.
    Proof. easy. Qed.

    Lemma empty_r_valid [Î£] (xs : DList Î£) : Valid xs -> xs âŠ£âŠ¢ empty.
    Proof. easy. Qed.

    Lemma valid_singleton [Î£] (F : Formula Î£) : Valid F -> Valid (singleton F).
    Proof. easy. Qed.

    Lemma error_l_unsatisfiable [Î£] (xs : DList Î£) : Unsatisfiable xs -> error âŠ£âŠ¢ xs.
    Proof. intros uxs Î¹. specialize (uxs Î¹). easy. Qed.

    Lemma error_r_unsatisfiable [Î£] (xs : DList Î£) : Unsatisfiable xs -> xs âŠ£âŠ¢ error.
    Proof. intros uxs Î¹. specialize (uxs Î¹). easy. Qed.

    Lemma unsatisfiable_singleton [Î£] (F : Formula Î£) :
      Unsatisfiable F -> Unsatisfiable (singleton F).
    Proof. apply unsatisfiable_snoc_r. Qed.

    Lemma singleton_formula_and [Î£] (F1 F2 : Formula Î£) :
      singleton (formula_and F1 F2) âŠ£âŠ¢ cat (singleton F1) (singleton F2).
    Proof. intro. now rewrite instprop_dlist_cat, !instprop_dlist_singleton. Qed.

    #[export] Instance proper_run [Î£] : Proper ((âŠ£âŠ¢) ==> (âŠ£âŠ¢)) (@run Î£).
    Proof. easy. Qed.

  End DList.

End FormulasOn.
