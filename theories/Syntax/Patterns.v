(******************************************************************************)
(* Copyright (c) 2019 Steven Keuchel                                          *)
(* All rights reserved.                                                       *)
(*                                                                            *)
(* Redistribution and use in source and binary forms, with or without         *)
(* modification, are permitted provided that the following conditions are     *)
(* met:                                                                       *)
(*                                                                            *)
(* 1. Redistributions of source code must retain the above copyright notice,  *)
(*    this list of conditions and the following disclaimer.                   *)
(*                                                                            *)
(* 2. Redistributions in binary form must reproduce the above copyright       *)
(*    notice, this list of conditions and the following disclaimer in the     *)
(*    documentation and/or other materials provided with the distribution.    *)
(*                                                                            *)
(* THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS        *)
(* "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED  *)
(* TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR *)
(* PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR          *)
(* CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,      *)
(* EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,        *)
(* PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR         *)
(* PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF     *)
(* LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING       *)
(* NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS         *)
(* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.               *)
(******************************************************************************)

From Katamaran Require Import
     Prelude
     Context
     Environment
     Syntax.TypeDef.

Import ctx.notations.
Import env.notations.

Module Type PatternsOn (Import TY : Types).

  Section Patterns.

    (* These patterns are used in program and logic contexts for pattern
       matching in statements and in assertions. We abstract over the type of
       variables here. *)
    Context {N : Set}.

    Inductive TuplePat : Ctx Ty -> NCtx N Ty -> Set :=
    | tuplepat_nil  : TuplePat Îµ Îµ
    | tuplepat_snoc
        {Ïƒs : Ctx Ty} {Î” : NCtx N Ty}
        (pat : TuplePat Ïƒs Î”) {Ïƒ : Ty} (x : N) :
        TuplePat (Ïƒs â–» Ïƒ) (Î” â–» xâˆ·Ïƒ).

    Inductive RecordPat : NCtx ğ‘¹ğ‘­ Ty -> NCtx N Ty -> Set :=
    | recordpat_nil  : RecordPat Îµ Îµ
    | recordpat_snoc
        {rfs : NCtx ğ‘¹ğ‘­ Ty} {Î” : NCtx N Ty}
        (pat : RecordPat rfs Î”) (rf : ğ‘¹ğ‘­) {Ï„ : Ty} (x : N) :
        RecordPat (rfs â–» rfâˆ·Ï„) (Î” â–» xâˆ·Ï„).

    Inductive Pattern : NCtx N Ty -> Ty -> Set :=
    | pat_var (x : N) {Ïƒ : Ty} : Pattern [ xâˆ·Ïƒ ] Ïƒ
    | pat_unit : Pattern Îµ ty_unit
    | pat_pair (x y : N) {Ïƒ Ï„ : Ty} : Pattern [ xâˆ·Ïƒ , yâˆ·Ï„ ] (ty_prod Ïƒ Ï„)
    | pat_tuple {Ïƒs Î”} (p : TuplePat Ïƒs Î”) : Pattern Î” (ty_tuple Ïƒs)
    | pat_record {R Î”} (p : RecordPat (ğ‘¹ğ‘­_Ty R) Î”) : Pattern Î” (ty_record R).

    Definition tuple_pattern_match_env {T : Ty -> Set} :
      forall {Ïƒs : Ctx Ty} {Î” : NCtx N Ty},
        TuplePat Ïƒs Î” -> Env T Ïƒs -> NamedEnv T Î” :=
      fix pattern_match {Ïƒs} {Î”} p {struct p} :=
        match p with
        | tuplepat_nil => fun _ => env.nil
        | tuplepat_snoc p x =>
          fun EÎ” =>
            match env.snocView EÎ” with
            | env.isSnoc E v => pattern_match p E â–º (_âˆ·_ â†¦ v)
            end
        end.

    Definition tuple_pattern_match_env_reverse {T : Ty -> Set} :
      forall {Ïƒs : Ctx Ty} {Î” : NCtx N Ty},
        TuplePat Ïƒs Î” -> NamedEnv T Î” -> Env T Ïƒs :=
      fix pattern_match {Ïƒs} {Î”} p {struct p} :=
        match p with
        | tuplepat_nil => fun _ => env.nil
        | tuplepat_snoc p x =>
          fun EÎ” =>
            match env.snocView EÎ” with
            | env.isSnoc E v => pattern_match p E â–º (_ â†¦ v)
            end
        end.

    Definition tuple_pattern_match_val {Ïƒs : Ctx Ty} {Î” : NCtx N Ty}
             (p : TuplePat Ïƒs Î”) : Val (ty_tuple Ïƒs) -> NamedEnv Val Î” :=
      fun lit => tuple_pattern_match_env p (@envrec.to_env Ty Val Ïƒs lit).

    Fixpoint record_pattern_match_env {V : Ty -> Set} {rfs : NCtx ğ‘¹ğ‘­ Ty} {Î” : NCtx N Ty}
             (p : RecordPat rfs Î”) {struct p} : NamedEnv V rfs -> NamedEnv V Î” :=
      match p with
      | recordpat_nil => fun _ => env.nil
      | recordpat_snoc p rf x =>
        fun E =>
          env.snoc
            (record_pattern_match_env p (env.tail E)) (xâˆ·_)
            (env.lookup E ctx.in_zero)
      end.

    Fixpoint record_pattern_match_env_reverse {V : Ty -> Set} {rfs : NCtx ğ‘¹ğ‘­ Ty} {Î” : NCtx N Ty}
             (p : RecordPat rfs Î”) {struct p} :  NamedEnv V Î” -> NamedEnv V rfs :=
      match p with
      | recordpat_nil => fun _ => env.nil
      | recordpat_snoc p rf x =>
        fun E =>
          env.snoc
            (record_pattern_match_env_reverse p (env.tail E)) (rfâˆ·_)
            (env.lookup E ctx.in_zero)
      end.

    Lemma record_pattern_match_env_inverse_right {V : Ty -> Set} {rfs : NCtx ğ‘¹ğ‘­ Ty} {Î” : NCtx N Ty}
          (p : RecordPat rfs Î”) (vs : NamedEnv V Î”) :
      record_pattern_match_env p (record_pattern_match_env_reverse p vs) = vs.
    Proof.
      induction p.
      - now destruct (env.nilView vs).
      - destruct (env.snocView vs) as [vs v].
        cbn. f_equal. now apply IHp.
    Qed.

    Lemma record_pattern_match_env_inverse_left {V : Ty -> Set} {rfs : NCtx ğ‘¹ğ‘­ Ty} {Î” : NCtx N Ty}
          (p : RecordPat rfs Î”) (vs : NamedEnv V rfs) :
      record_pattern_match_env_reverse p (record_pattern_match_env p vs) = vs.
    Proof.
      induction p.
      - now destruct (env.nilView vs).
      - destruct (env.snocView vs) as [vs v].
        cbn. f_equal. now apply IHp.
    Qed.

    Lemma tuple_pattern_match_env_inverse_right {T : Ty -> Set}
      {Ïƒs : Ctx Ty} {Î” : NCtx N Ty} (p : TuplePat Ïƒs Î”) (ts : NamedEnv T Î”) :
      tuple_pattern_match_env p (tuple_pattern_match_env_reverse p ts) = ts.
    Proof.
      induction p; cbn.
      - now destruct (env.nilView ts).
      - destruct (env.snocView ts); cbn.
        now rewrite (IHp E).
    Qed.

    Lemma tuple_pattern_match_env_inverse_left {T : Ty -> Set}
          {Ïƒs : Ctx Ty} {Î” : NCtx N Ty} (p : TuplePat Ïƒs Î”) (ts : Env T Ïƒs) :
      tuple_pattern_match_env_reverse p (tuple_pattern_match_env p ts) = ts.
    Proof.
      induction p.
      - now destruct (env.nilView ts).
      - destruct (env.snocView ts); cbn.
        now rewrite (IHp E).
    Qed.

    Definition record_pattern_match_val {R} {Î” : NCtx N Ty}
      (p : RecordPat (ğ‘¹ğ‘­_Ty R) Î”) : Val (ty_record R) -> NamedEnv Val Î” :=
      fun v => record_pattern_match_env p (ğ‘¹_unfold v).

    Definition pattern_match_val {Ïƒ : Ty} {Î” : NCtx N Ty} (p : Pattern Î” Ïƒ) :
      Val Ïƒ -> NamedEnv Val Î” :=
      match p with
      | pat_var x => fun v => env.snoc env.nil (xâˆ·_) v
      | pat_unit => fun _ => env.nil
      | pat_pair x y => fun '(u , v) => env.snoc (env.snoc env.nil (xâˆ·_) u) (yâˆ·_) v
      | pat_tuple p => tuple_pattern_match_val p
      | pat_record p => record_pattern_match_val p
      end.

    Definition pattern_match_env_val_reverse {Ïƒ : Ty} {Î” : NCtx N Ty} (p : Pattern Î” Ïƒ) :
      NamedEnv Val Î” -> Val Ïƒ :=
      match p with
      | pat_var x    => fun Ex => match env.snocView Ex with env.isSnoc _ t => t end
      | pat_unit     => fun _ => (tt : Val ty_unit)
      | pat_pair x y => fun Exy => match env.snocView Exy with
                                     env.isSnoc Ex ty =>
                                     match env.snocView Ex with
                                       env.isSnoc _ tx => (pair tx ty : Val (ty_prod _ _))
                                     end
                                   end
      | pat_tuple p  => fun EÎ” => (envrec.of_env (tuple_pattern_match_env_reverse p EÎ”) : Val (ty_tuple _))
      | pat_record p => fun EÎ” => (ğ‘¹_fold (record_pattern_match_env_reverse p EÎ”) : Val (ty_record _))
      end.

    Lemma pattern_match_val_inverse_left {Ïƒ : Ty} {Î” : NCtx N Ty} {p : Pattern Î” Ïƒ}
          (v : Val Ïƒ) :
      pattern_match_env_val_reverse p (pattern_match_val p v) = v.
    Proof.
      induction p; cbn; eauto.
      - now destruct v.
      - now destruct v.
      - unfold tuple_pattern_match_val.
        now rewrite tuple_pattern_match_env_inverse_left, envrec.of_to_env.
      - unfold record_pattern_match_val.
        now rewrite record_pattern_match_env_inverse_left, ğ‘¹_fold_unfold.
    Qed.

    Lemma pattern_match_val_inverse_right {Ïƒ : Ty} {Î” : NCtx N Ty} (p : Pattern Î” Ïƒ)
      (vs : NamedEnv Val Î”) :
      pattern_match_val p (pattern_match_env_val_reverse p vs) = vs.
    Proof.
      induction p; cbn; eauto.
      - destruct (env.snocView vs).
        now destruct (env.nilView E).
      - now destruct (env.nilView vs).
      - destruct (env.snocView vs).
        destruct (env.snocView E).
        now destruct (env.nilView E).
      - unfold tuple_pattern_match_val.
        now rewrite envrec.to_of_env, tuple_pattern_match_env_inverse_right.
      - unfold record_pattern_match_val.
        now rewrite ğ‘¹_unfold_fold, record_pattern_match_env_inverse_right.
    Qed.

  End Patterns.

  Bind Scope pat_scope with TuplePat.
  Bind Scope pat_scope with RecordPat.
  Bind Scope pat_scope with Pattern.

End PatternsOn.
